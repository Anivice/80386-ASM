# Chapter 3.1: Assembly Example `movsb`

In Chapter 3, we covered techniques for manipulating VRAM. However,
setting VRAM bytes individually can be a lengthy and tedious process.

That's why I give my kids `movsb`/`movsw`.

![](./thats_why_i_give_my_kids_movsb.jpeg)

This allows them to efficiently transfer large blocks of contiguous data between memory
locations, much like how `memcpy()` is used in higher-level programming languages,
without giving them schizophrenia and bipolar disorder.

## Workflow of Using `movsb`

The `movsb` instruction is used for efficient block data transfers between memory
locations. It relies on four critical components to function correctly:

1. **`DS:SI`**: Points to the source data (e.g., the start address of the message string).
2. **`ES:DI`**: Points to the destination (e.g., the start of the VRAM for text display).
3. **`CX`**: The number of bytes to transfer.
4. **`DF`**: traversal direction of the two buffers, DF=0 means forward while DF=1 means
             backward.

### 1. Setting `DS:SI` (Source Address)

The `DS:SI` specifies the starting address of the source data in memory.
For `movsb`, the source data is read from `[DS:SI]`.

### 2. Setting `ES:DI` (Destination Address)

The `ES:DI` pair specifies the starting address of the destination memory
where the data will be written.
For `movsb`, the destination data is written to `[ES:DI]`.

### 3. Setting `CX` (Count of Bytes to Transfer)

The `CX` register is used by the `rep` prefix to specify the number of iterations
for repeated instructions like `movsb`.

### **4. Setting `DF` (Direction Flag)**

The `DF` (Direction Flag) controls how `SI` and `DI` are adjusted after each data
transfer:

- **When `DF = 0` (via `cld`)**: Both `SI` and `DI` increment after copying the data, 
                                 enabling forward traversal through memory.
- **When `DF = 1` (via `std`)**: Both `SI` and `DI` decrement after copying the data,
                                 enabling backward traversal through memory.

---

## Assembly Code

```nasm
1   [bits 16]                                       ; 16-bit mode
```
The first line of the code is `[bits 16]`. This directive tells NASM that the
following instructions should be assembled for a 16-bit environment.

---

```nasm
2   StartingPoint equ 0x7C00                        ; Boot sector loads at 0x7C00
```
This line uses the `equ` directive in NASM, which functions similarly to `#define`
in C and C-like languages. It allows you to define a constant value or label that
remains fixed throughout the code. In this case, `StartingPoint equ 0x7C00` defines 
`StartingPoint` as a symbolic constant for the memory address `0x7C00`.

---

```nasm
3   [org StartingPoint]
```
This line defines the starting point of the code.
In real mode, code execution is addressed linearly within a small vicinity of memory
(inside the segmentation).
Although we know that the code will be loaded at `0x0000:0x7C00`,
NASM does not have this context by default. Without our interference, NASM will assume
that the code starts at offset `0x0000` and calculate all offsets, labels, and references
like `$` relative to this incorrect starting point (`0x00`). This misalignment would lead
to errors when the program is executed since BIOS loads the program at (`0x0000:0x7C00`).
To prevent this, we explicitly inform NASM of the code's actual memory location using
the `[org]` directive, so it correctly calculates offsets and labels based on the proper
load address.

---

```nasm
4   ; jump over the data section
5   jmp start
```
Now, we jump to the `start` label.
Immediately following `start` is the data section.
The way data sections are handled in MBR assembly is vastly different from
how they are managed in C code. In C, the compiler and operating system manage
separate memory regions for code and data, ensuring that the processor doesn’t
mistakenly execute data as instructions. However, in assembly, particularly in MBR
code, the processor has no such capability to distinguish between instructions and
data. It simply executes whatever is in memory. To avoid accidental execution of the
data section as code, we explicitly jump over the data section at the start of the
program. This ensures the processor begins executing the intended instructions while
skipping over the embedded data. (And, as a hacking solution, we can add data to our
code if I wish to execute an instruction sequence that is not recognized by NASM.)

---

```nasm
6   ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
7   ; Message:
8   msg:
9       db 'H', 0x07, 'e', 0x07, 'l', 0x07, 'l', 0x07, 'o', 0x07, ',', 0x07, ' ', 0x07
10      db 'W', 0x07, 'o', 0x07, 'r', 0x07, 'l', 0x07, 'd', 0x07, '!', 0x07
11  msg_len:
12      dw $ - msg                                  ; Length of the message
13  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
```
Here is the data section. The `db` directive is used to define 8-bit data (bytes),
while `dw` is used to define 16-bit data (words). We use the `msg` label to mark
the starting address of the data section and `msg_len` to calculate the length of
the message in bytes. At first glance, this setup seems correct; however, there
are several important issues that need to be addressed, particularly concerning
how the data is accessed and managed. These challenges will be discussed
in detail later.

---

```nasm
14  start:
15      ; Setup data segment
16      mov ax, 0x07C0                              ; Segment of the bootloader
17      mov ds, ax
18      ; Setup source [DS:SI]
19      mov si, msg - StartingPoint                 ; SI points to the message
```
Now, let's examine the code section. We set the `ds` (Data Segment) register to
`0x07C0`. In this example, we use the segmentation starting from `0x07C0` instead of
`0x0000`. This can be confusing and unnecessary, but here we use this as an example
to show how segmentation can be managed in different contexts.
Now, the segment value `0x07C0` is shifted left by $4$ bits and then the offset is added
to determine the physical memory address. Since the code is loaded at `0x0000:0x7C00`,
i.e., `0x07C00` in linear addressing, the segment part is `0x07C0` and the index offset
should starts with `0x0000`

Why load `0x07C0` into `ax` and then transfer it to `ds`?
The reason behind this is that the designers used their feet instead of their brain, 
causing us to not able to directly assign a constant value to the `ds` register.
Segment registers like `ds` can only be loaded indirectly through general-purpose
registers such as `ax`.

Next, we need to set `si` (Source Index) with the correct offset.
At first glance, assigning `msg` to `si` seems correct, but it doesn't account
for the fact that we have set our segmentation starting point to `0x07C0`.
All offsets used by this addressing scheme should start from `0x0000` instead of
`0x7C00`.

---

```nasm
20      ; Setup destination [ES:DI] (Video memory at 0xB8000)
21      mov ax, 0xB800                              ; Segment of video memory
22      mov es, ax
23      xor di, di                                  ; DI starts at offset 0
```
The same logic applies to `es:di`.
In this context, we point it to `0xB8000`, the starting address of Video RAM (VRAM).
Setting `es` to `0xB800` establishes the segmentation starting address for VRAM,
and we cleared `di` (Destination Index) so that it points to `0x0000`.

---

```nasm
24      ; Set CX to the length of the message
25      mov cx, [ds:msg_len - StartingPoint]        ; CX = length of the message in bytes
```
We set `cx` to the length of the data, which is provided by
`[ds:msg_len - StartingPoint]`.

---

```nasm
26      ; Ensure forward direction
27      cld                                         ; Clear the Direction Flag
```
By using the `cld` (Clear Direction Flag) instruction, we set `DF` to `0`, to ensure
`movsb` to operate in forward mode.

---

```nasm
28      ; Copy message to video memory
29      rep movsb                                   ; Copy CX bytes from [DS:SI] to [ES:DI]
```
The combined `rep movsb` operation copies `CX` bytes from the source
location `[DS:SI]` to the destination location `[ES:DI]`. By clearing the Direction
Flag (`DF`) using `cld`, the processor ensures forward traversal of memory,
incrementing `SI` and `DI` after each copy.

---

```nasm
30      ; Halt
31      jmp $                                       ; Infinite loop
32  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
```
Halt the system by entering an infinite loop.

---

We set the boot sector signature here so that BIOS recognize us as a bootable device:
```nasm
33  ; Padding to fill the boot sector
34  times 510-($-$$) db 0                           ; Fill with zeros
35  dw 0xAA55                                       ; Boot sector signature
```

## Debug

Now, let’s perform a simple debugging process. In the CMake binary directory,
assuming you are using the Ninja build system, you can start debugging by
running the following command:
```bash
    ninja debug_chapter3.1
```
QEMU offers an interactive console for system control and a graphical
user interface (GUI) to display output.
![Ninja Debug print.bin](ninja_debug_print.png)
![QEMU](qemu_debug_print.png)

As you can see, QEMU starts in a paused state and will remain paused
until it receives instructions from GDB to proceed.

Launch GDB using the following command:
```bash
    # I'm assuming you are still in ${CMAKE_BINARY_DIR}
    # Like I said, this script can be executed with relative path even though its
    # resource files are in its own directory.
    ../gdb/debug_in_real_mode.sh
```
![Debug in Real Mode](real_mode_gdb.png)

GDB will automatically connect to QEMU (gdbserver at tcp://127.0.0.1:1234) and set
a breakpoint at `0x0000:0x7C00`.
Use the `continue` command to run the BIOS-ROM until it pauses at `0x0000:0x7C00`.
From there, use `stepi` to step through the instructions.
Each execution of `stepi` automatically displays the current context on the screen
using `context` command.

[Here is a video illustrating this process, if you like.](https://youtu.be/p5AlnEL4SXY).

---

[Chapter 3.2](3.2_conditional_jump.md)

[Back to the Main Page](../README.md)

